pkgname=mingw-w64-tcl
pkgver=8.6.7
pkgrel=1
pkgdesc="The Tcl scripting language (mingw-w64)"
arch=(any)
depends=(mingw-w64-crt mingw-w64-zlib)
makedepends=(mingw-w64-gcc tcl)
options=(staticlibs !strip !buildflags)
license=("custom")
url="http://tcl.sourceforge.net"
source=("http://downloads.sourceforge.net/sourceforge/tcl/tcl${pkgver}-src.tar.gz"
"tcl-8.5.14-autopath.patch"
"tcl-8.5.14-hidden.patch"
"tcl-mingw-w64-compatibility.patch"
"tcl-system-zlib.patch"
"tcl-fix-using-gnu-print.patch"
"tcl-fix-shared-linking.patch"
"tcl-fix-proper-implib-name.patch"
"tcl-fix-installation.patch"
"tcl-fix-forbidden-colon-in-paths.patch"
"tcl-disable-xc.patch")
sha256sums=('7c6b8f84e37332423cfe5bae503440d88450da8cc1243496249faa5268026ba5'
            '3cb435f768052acabe5b1fbef5ae9a8d0967b0f86f7695fb4bae8953ee470357'
            'a809617939336a0068c5dd567f2c25489b2824ca25ad20f9cbf4e74c81396d2e'
            '866fe923b7d43339bea8a8484299ba71ebdb3a7213508809f2adae4eff60a382'
            '931485d71969096c1d03c8bed24fae3922d143fe50820d913e2567492ad6ac41'
            '9c66ffe2de1d543f5291367d562ed5ee94e7e67345b281605788d7d9e02b8e7b'
            'a01e70e0277cafc6ccc91415858cf101b237f2e116206a9448d21cba8879abdc'
            '5c0162fbb018c03b3e4b907bd0098ab5282314bc212e3929a0416126637e1350'
            'f1833c3164229b017417d2ab2ce4cb066252fc1ad256de2313f0239481c7cc37'
            '70bf0d8e84985f4e8ee63447ad37d5e50376eaf35ace51112761cacbbd596c4c'
            '2b0f41f6704aa964dbfafa0a65dd5ce0ab97e82ff5cbbe2a95a2e8d644cc5550')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare() {
  cd "${srcdir}/tcl${pkgver}"
  patch -Np1 -i "${srcdir}/tcl-8.5.14-autopath.patch"
  patch -Np1 -i "${srcdir}/tcl-8.5.14-hidden.patch"
  patch -Np0 -i "${srcdir}/tcl-mingw-w64-compatibility.patch"
  patch -Np1 -i "${srcdir}/tcl-system-zlib.patch"
  patch -Np1 -i "${srcdir}/tcl-fix-using-gnu-print.patch"
  patch -Np1 -i "${srcdir}/tcl-fix-shared-linking.patch"
  patch -Np1 -i "${srcdir}/tcl-fix-proper-implib-name.patch"
  patch -Np1 -i "${srcdir}/tcl-fix-installation.patch"
  patch -Np1 -i "${srcdir}/tcl-fix-forbidden-colon-in-paths.patch"
  patch -Np1 -i "${srcdir}/tcl-disable-xc.patch"
}

build() {
  for _arch in ${_architectures}; do
		unset LDFLAGS
    mkdir -p "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    cp -r "$srcdir/tcl$pkgver/"* "${srcdir}/${pkgname}-${pkgver}-build-${_arch}/"
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    [ "${_arch}" = 'x86_64-w64-mingw32' ] && enable64bit='--enable-64bit'
    "${srcdir}"/${pkgname#mingw-w64-}${pkgver}/win/configure \
      --prefix=/usr/${_arch} \
      --build=$CHOST \
      --enable-threads \
      --host=${_arch} $enable64bit
    make
  done
}

package() {
	for _arch in ${_architectures}; do
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    make -j1 install INSTALL_ROOT="$pkgdir"
    find "$pkgdir/usr/${_arch}" -name '*.exe' -o -name '*.bat' -o -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
    find "$pkgdir/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip --strip-unneeded
    find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
    find "$pkgdir/usr/${_arch}" -name '*.sh' | xargs -rtl1 chmod 755
    find "$pkgdir/usr/${_arch}" -name '*.sh' | xargs -rtl1 sed -i "s,libtcl86.a,libtcl86.dll.a,g"
    rm -r "$pkgdir/usr/${_arch}/"{man,share}
    sed -e "s#${srcdir}/tcl${pkgver}/win#/usr/lib#" \
      -e "s#${srcdir}/tcl${pkgver}#/usr/${_arch}/include/tcl-private#" \
      -i "${pkgdir}/usr/${_arch}/lib/tclConfig.sh"
		sed -e "s#${srcdir}/tcl${pkgver}/win/pkgs/tdbc1.0.5#/usr/${_arch}/lib/tdbc1.0.0#" \
				-e "s#${srcdir}/tcl${pkgver}/pkgs/tdbc1.0.5/generic#/usr/${_arch}/include#" \
				-e "s#${srcdir}/tcl${pkgver}/pkgs/tdbc1.0.5/library#/usr/${_arch}/lib/tcl${pkgver%.*}#" \
				-e "s#${srcdir}/tcl${pkgver}/pkgs/tdbc1.0.5#/usr/${_arch}/include#" \
				-i "${pkgdir}/usr/${_arch}/lib/tdbc1.0.5/tdbcConfig.sh"
		sed -e "s#${srcdir}/tcl${pkgver}/win/pkgs/itcl4.1.0#/usr/${_arch}/lib/itcl4.0.0#" \
				-e "s#${srcdir}/tcl${pkgver}/pkgs/itcl4.1.0/generic#/usr/${_arch}/include/tcl-private#" \
				-e "s#${srcdir}/tcl${pkgver}/pkgs/itcl4.1.0#/usr/${_arch}/include/tcl-private#" \
				-i "${pkgdir}/usr/${_arch}/lib/itcl4.1.0/itclConfig.sh"
		mv "$pkgdir/usr/${_arch}/lib/libtcl86.a" "$pkgdir/usr/${_arch}/lib/libtcl86.dll.a"
		ln -s "/usr/${_arch}/lib/libtcl86.dll.a" "$pkgdir/usr/${_arch}/lib/libtcl.dll.a"
		ln -s /usr/${_arch}/lib/tclConfig.sh "${pkgdir}/usr/${_arch}/lib/tcl${pkgver%.*.*}/tclConfig.sh"
    mkdir -p "${pkgdir}/usr/${_arch}/include/tcl-private/"{generic,win}
    find generic win -name "*.h" -exec cp -p '{}' "${pkgdir}"/usr/${_arch}/include/tcl-private/'{}' ';'
    ( cd "${pkgdir}/usr/${_arch}/include"
      for i in *.h ; do
        [ -f "${pkgdir}/usr/${_arch}/include/tcl-private/generic/$i" ] && ln -sf ../../$i "${pkgdir}/usr/${_arch}/include/tcl-private/generic" ;
      done
    ) || true
    chmod a-x "${pkgdir}/usr/${_arch}/lib/tcl${pkgver%.*}/encoding/"*.enc
    chmod a-x "${pkgdir}/usr/${_arch}/lib/"*/pkgIndex.tcl
  done
}

# vim:set ts=2 sw=2 et:
