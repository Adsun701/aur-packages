# Maintainer: Andrew Sun <adsun701@gmail.com>

pkgname=mingw-w64-freealut
_pkgname=freealut
pkgver=1.1.0
pkgrel=1
pkgdesc="OpenAL Utility Toolkit (ALUT) (mingw-w64)"
arch=('any')
url="http://www.openal.org"
license=("LGPL")
makedepends=('mingw-w64-configure')
depends=('mingw-w64-openal')
options=('!strip' '!buildflags' 'staticlibs')
_md5sums=e089b28a0267faabdb6c079ee173664a
source=("http://pkgs.fedoraproject.org/repo/pkgs/freealut/${_pkgname}-${pkgver}.tar.gz/${_md5sums}/${_pkgname}-${pkgver}.tar.gz")
sha512sums=('270f74972548b4ac6b98c52c51787ed43c20cf79979063d073bbee7bd08ac4f34c2b579fbf15c09c4e606a5ed38dcd0252f5c46fb3cfe43b727b6b53cf747933')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  autoreconf -fiv
}

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  for _arch in ${_architectures}; do
    mkdir -p build-${_arch} && pushd build-${_arch}
    ${_arch}-configure ..
    make
    popd
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/${_pkgname}-${pkgver}/build-${_arch}"
    make DESTDIR="${pkgdir}" install
    ${_arch}-strip --strip-unneeded "$pkgdir"/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g "$pkgdir"/usr/${_arch}/lib/*.a
  done
}
